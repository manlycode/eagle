*
.SUBCKT 74HC4051   VCC VEE GND INH A2 A1 A0 Y0 Y1 Y2 Y3 Y4 Y5 Y7 Y7 Z
********************************************************************************
*Node VCC         +Vsupply
*Node VEE         -Vsupply
*Node GND         Ground
*Node INH         Inhibit
*Node A2-A0        A2-A0 Control Inputs
*Node Y0-Y7        Mux Input/Output 1 - Mux Input/Output 8
*Node Z        Common Input/Output
*******************************************************************************
**** Convert Analog Control Input to Digital ***
aADCInh [INH] [INHd]  AtoD
aADC2  [A2] [A2d]  AtoD
aADC1  [A1] [A1d]  AtoD
aADC0  [A0] [A0d]  AtoD
****Decode Digital Input*********
aDEC [INHd A2d A1d A0d] [Control1d Control2d Control3d Control4d Control5d Control6d Control7d Control8D] MUXDIGDEC

***Convert Digital Control Signals to Analog control voltages ***
aDAC1 [Control1d] [Control1] DtoA
aDAC2 [Control2d] [Control2] DtoA
aDAC3 [Control3d] [Control3] DtoA
aDAC4 [Control4d] [Control4] DtoA
aDAC5 [Control5d] [Control5] DtoA
aDAC6 [Control6d] [Control6] DtoA
aDAC7 [Control7d] [Control7] DtoA
aDAC8 [Control8d] [Control8] DtoA
**** Mux Input Select Switches ****
aSW1  Control1 ( Y0 Z )  MC74HC4051
aSW2  Control2 ( Y1 Z )  MC74HC4051
aSW3  Control3 ( Y2 Z )  MC74HC4051
aSW4  Control4 ( Y3 Z )  MC74HC4051
aSW5  Control5 ( Y4 Z )  MC74HC4051
aSW6  Control6 ( Y5 Z )  MC74HC4051
aSW7  Control7 ( Y6 Z )  MC74HC4051
aSW8  Control8 ( Y7 Z )  MC74HC4051
*** Diode Protection ***
*D1  VCC Y0 D_Protect
*D2  Y0 VEE D_Protect
*D3  VCC Y1 D_Protect
*D4  Y1 VEE D_Protect
*D5  VCC Y2 D_Protect
*D6  Y2 VEE D_Protect
*D7  VCC Y3 D_Protect
*D8  Y3 VEE D_Protect
*D9  VCC Y4 D_Protect
*D10 Y4 VEE D_Protect
*D11 VCC Y5 D_Protect
*D12 Y5 VEE D_Protect
*D13 VCC Y6 D_Protect
*D14 Y6 VEE D_Protect
*D15 VCC Y7 D_Protect
*D16 Y7 VEE D_Protect
*D17 VCC Z  D_Protect
*D18 Z  VEE D_Protect
.MODEL MUXDIGDEC d_chip ( behaviour= "
+/inputs INH A2 A1 A0
+/outputs D1 D2 D3 D4 D5 D6 D7 D8
+/table 9
+; CONTROL IN     DATA OUT
+;INH A2 A1 A0 1 2 3 4 5 6 7 8
+  L  L  L  L  H L L L L L L L 
+  L  L  L  H  L H L L L L L L
+  L  L  H  L  L L H L L L L L
+  L  L  H  H  L L L H L L L L
+  L  H  L  L  L L L L H L L L
+  L  H  L  H  L L L L L H L L
+  L  H  H  L  L L L L L L H L
+  L  H  H  H  L L L L L L L H
+  H  X  X  X  L L L L L L L L
+")
.MODEL D_Protect D()
.MODEL AtoD adc_bridge (in_low= 0.8  in_high = 3.8)
.MODEL DtoA dac_bridge (out_low= 0.0 out_high= 5.0)
.MODEL MC74HC4051  aswitch  (cntl_off= 0.5  cntl_on= 3.5
+                       r_off= 1e7  r_on= 80 log=TRUE )
.ENDS 74HC4051